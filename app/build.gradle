/*
 * MythTV Player An application for Android users to play MythTV Recordings and Videos
 * Copyright (c) 2015. Daniel Frey
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {

    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "org.mythtv.android"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 15
        versionName "1.1.12"
        archivesBaseName = applicationId + "-" + versionName
    }

    if( project.hasProperty( "MythtvPlayer.properties" )
            && new File( project.property( "MythtvPlayer.properties" ) ).exists() ) {

        Properties props = new Properties()
        props.load( new FileInputStream( file( project.property( "MythtvPlayer.properties" ) ) ) )

        signingConfigs {
            release {
                storeFile file( props[ 'keystore' ] )
                storePassword props[ 'keystore.password' ]
                keyAlias props[ 'keyAlias' ]
                keyPassword props[ 'keyPassword' ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile( 'proguard-android.txt' ), 'proguard-rules.pro'
            if( project.hasProperty( "MythtvPlayer.properties" ) )
                signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:leanback-v17:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile project(':core')
}

apply from: "${project(':').projectDir}/config/static_analysis.gradle"
