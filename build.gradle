/*
 * MythTV Player An application for Android users to play MythTV Recordings and Videos
 * Copyright (c) 2015. Daniel Frey
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath "com.google.guava:guava:19.0"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.google.firebase:firebase-plugins:1.0.4'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }

}

allprojects {

    repositories {

        jcenter()

    }

    project.ext {

        //Android
        buildToolsVersion = '25.0.2'
        minSdkVersion = 17
        targetSdkVersion = 25
        compileSdkVersion = 25

        //Android Support Libraries
        supportVersion = '25.0.1'
        playServicesVersion = '10.0.1'
        firebaseVersion = '10.0.1'

        //Libraries
        daggerVersion = '2.6'
        butterKnifeVersion = '8.4.0'
        rxJavaVersion = '1.2.3'
        rxAndroidVersion = '1.2.1'
        javaxAnnotationVersion = '1.0'
        gsonVersion = '2.7'
        okHttpVersion = '3.5.0'
        picassoVersion = '2.5.2'
        jodaVersion = '2.9.6'
        exoplayerVersion = 'r1.5.4'
        glideVersion = '3.7.0'
        multidexVersion = '1.0.1'
        recyclerviewfastscrollerVersion = "0.1.3"
        stethoVersion = '1.4.1'
        leakCanaryVersion = '1.4-beta2'
        androidQueryVersion = '0.25.9'
        cclVersion = '2.8.4'

        //Testing
        robolectricVersion = '3.0'
        jUnitVersion = '4.12'
        hamcrestVersion = '1.3'
        assertJVersion = '3.3.0'
        mockitoVersion = '2.2.29'
        dexmakerVersion = '1.2'
        espressoVersion = '2.3-alpha'
        testingSupportRunnerVersion = '0.4.1'
        testingSupportRulesVersion = '0.6-alpha'
        testingSupportuiVersion = '2.1.2'
        multidexInstrumentationVersion = '1.0.1'

    }

}

/*
    Version Code Algorithm
    ----------------------
    Version Code = Min API Level + Min Screen Size + Max Screen Size + App Version

    example: 1414210


    Android Version Table
    ---------------------
    API Level | Version Number | Code Name
    ------------------------------------------------------------------------------------------------
    14 - 15   | 4.0 - 4.0.4    | Ice Cream Sandwich
    16 - 18   | 4.1 - 4.3.1    | Jellybean
    19 - 20   | 4.4 - 4.4.4    | KitKat
    21 - 22   | 5.0 - 5.1.1    | Lollipop
    23        | 6.0 - 6.0.1    | Marshmellow
    24        | 7.0 - 7.0.1    | Nougat
    25        | 7.1?           | Nougat

    Android Screen Size Table
    --------------------------
    Code | Size
    ------------------------------------------------------------------------------------------------
    1    | low
    2    | normal
    3    | large
    4    | xlarge

 */

    ext {
        androidApplicationId = 'org.mythtv.android'
        androidVersionCode = 1714300
        androidVersionName = "3.0.0"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = 'org.mythtv.android.test'
    }

if( project.hasProperty( "mythtvplayer.google-cast" )
        && new File( project.property( "mythtvplayer.google-cast" ) + ".gradle" ).exists() ) {

    apply from: project.property( "mythtvplayer.google-cast" ) + ".gradle";

}

project.ext.preDexLibs = !project.hasProperty( 'disablePreDex' )

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if( "com.android.build.gradle.AppPlugin".equals(plugin.class.name ) ) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if( "com.android.build.gradle.LibraryPlugin".equals( plugin.class.name ) ) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task wrapper( type: Wrapper ) {
    description 'Creates the gradle wrapper.'
    gradleVersion '3.3'
}
