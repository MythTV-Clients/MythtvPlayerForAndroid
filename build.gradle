/*
 * MythTV Player An application for Android users to play MythTV Recordings and Videos
 * Copyright (c) 2015. Daniel Frey
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath "com.google.guava:guava:20.0"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.google.firebase:firebase-plugins:1.0.4'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }

}

allprojects {

    repositories {

        jcenter()

    }

    project.ext {

        //Android Support Libraries
        supportVersion = '25.3.1'
        playServicesVersion = '10.2.1'
        firebaseVersion = '10.0.1'

        //Libraries
        autoValueVersion = '1.4'
        autoValueGsonVersion = '0.4.6'
        daggerVersion = '2.6'
        butterKnifeVersion = '8.4.0'
        rxJavaVersion = '1.2.3'
        rxAndroidVersion = '1.2.1'
        javaxAnnotationVersion = '1.0'
        gsonVersion = '2.8.0'
        okHttpVersion = '3.5.0'
        picassoVersion = '2.5.2'
        jodaVersion = '2.9.6'
        exoplayerVersion = 'r1.5.4'
        glideVersion = '3.7.0'
        multidexVersion = '1.0.1'
        recyclerviewfastscrollerVersion = "0.1.3"
        stethoVersion = '1.4.1'
        leakCanaryVersion = '1.4-beta2'
        androidQueryVersion = '0.25.9'
        cclVersion = '2.8.4'
        fabCircleProgressVersion = '1.01@aar'

        //Testing
        robolectricVersion = '3.0'
        jUnitVersion = '4.12'
        hamcrestVersion = '1.3'
        assertJVersion = '3.3.0'
        mockitoVersion = '2.2.29'
        dexmakerVersion = '1.2'
        espressoVersion = '2.3-alpha'
        testingSupportRunnerVersion = '0.4.1'
        testingSupportRulesVersion = '0.6-alpha'
        testingSupportuiVersion = '2.1.2'
        multidexInstrumentationVersion = '1.0.1'

    }

}

if( project.hasProperty( "mythtvplayer.google-cast" )
        && new File( project.property( "mythtvplayer.google-cast" ) + ".gradle" ).exists() ) {

    apply from: project.property( "mythtvplayer.google-cast" ) + ".gradle";

}

project.ext.preDexLibs = !project.hasProperty( 'disablePreDex' )

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if( "com.android.build.gradle.AppPlugin".equals(plugin.class.name ) ) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if( "com.android.build.gradle.LibraryPlugin".equals( plugin.class.name ) ) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task wrapper( type: Wrapper ) {
    description 'Creates the gradle wrapper.'
    gradleVersion '3.4'
}
